---
_id: 563d9b67bf652a600632d058
title: gulpでSVGをWebフォント化する
create: '2015-03-10 18:00'
modify: '2017-03-01 08:57'
categories:
  - Node.js
  - gulp
  - io.js
image: >-
  https://raw.githubusercontent.com/YuG1224/gulp-task-svg-webify/master/doc/sample.png
---

[YuG1224/gulp-task-svg-webify](https://github.com/YuG1224/gulp-task-svg-webify)

Webフォント化するために[IcoMoon](https://icomoon.io/)にSVGをアップロードするのが面倒なので、SVG画像をminifyして、Webフォント化するgulpタスクを作りました。

参考にしたのはここ。

<iframe class="bookmarklet hatena-embed" src="http://hatenablog.com/embed?url=http%3A%2F%2Fwhiskers.nukos.kitchen%2F2014%2F12%2F23%2Fgulp-consolidate.html" title="これからはじめるGulp（23）：gulp-consolidateでgulp-iconfontで作ったアイコンフォントのシンボル一覧HTMLを作る | Webデザイン、フロントエンド系の技術に関する備忘録 - whiskers" style="border:none;display:block;margin:0 0 1.7rem;overflow:hidden;height:155px;width:100%;max-width:468px;"><a href="http://whiskers.nukos.kitchen/2014/12/23/gulp-consolidate.html" target="_blank">これからはじめるGulp（23）：gulp-consolidateでgulp-iconfontで作ったアイコンフォントのシンボル一覧HTMLを作る | Webデザイン、フロントエンド系の技術に関する備忘録 - whiskers</a></iframe>

<!-- more -->

## 構造

全体の構造は以下。

`src/svg/`以下にSVG画像を置けば、`dist`以下に生成される仕組みになっています。

``` bash
.
├── README.md
├── dist
│   ├── css
│   ├── fonts
│   ├── index.html
│   └── svg
├── doc
├── gulpfile.coffee
├── node_modules
├── package.json
└── src
    ├── svg
    ├── template.css
    └── template.html
```

## gulp minify

svgのminifyには`gulp-svgmin`を使います。

`./src/svg/`以下に置いたSVGを圧縮して、`./dist/svg/`以下に出力します。

``` coffee
gulp = require "gulp"
foreach = require "gulp-foreach"
svgmin = require "gulp-svgmin"
concat = require "gulp-concat"

# minify
gulp.task "minify", () ->
  return gulp.src ["src/svg/*.svg"]
    .pipe foreach (stream, file) ->
      filename = file.path.replace file.base, ""
      stream.pipe svgmin()
        .pipe concat filename
    .pipe gulp.dest "dist/svg"
```

## gulp webify

minifyしたSVGを元に`gulp-iconfont`と`gulp-consolidate`でwebfont化します。

template用のcssとhtmlは[cognitom/symbols-for-sketch](https://github.com/cognitom/symbols-for-sketch/tree/master/templates)にあるtemplateを自分が好きなHandlebars形式に改変して使わせて頂きました。

Webフォント化した時にデフォルトではUnicodeの私用領域の最初E001から埋めていくのですが、SoftBankの絵文字と衝突してしまい環境によっては正しく表示されなくなります。

<iframe class="bookmarklet hatena-embed" src="http://hatenablog.com/embed?url=http%3A%2F%2Fja.wikipedia.org%2Fwiki%2F%25E6%2590%25BA%25E5%25B8%25AF%25E9%259B%25BB%25E8%25A9%25B1%25E3%2581%25AE%25E7%25B5%25B5%25E6%2596%2587%25E5%25AD%2597" title="携帯電話の絵文字 - Wikipedia" style="border:none;display:block;margin:0 0 1.7rem;overflow:hidden;height:155px;width:100%;max-width:468px;"><a href="http://ja.wikipedia.org/wiki/%E6%90%BA%E5%B8%AF%E9%9B%BB%E8%A9%B1%E3%81%AE%E7%B5%B5%E6%96%87%E5%AD%97" target="_blank">携帯電話の絵文字 - Wikipedia</a></iframe>

スマホ利用もあると考えて、`options.startCodepoint`を`0xF001`にしてみました。

``` coffee
gulp = require "gulp"
foreach = require "gulp-foreach"
iconfont = require "gulp-iconfont"
consolidate = require "gulp-consolidate"
concat = require "gulp-concat"
rs = require("run-sequence").use(gulp)

options =
  fontName: "webfont"
  className: "wf"
  startCodepoint: 0xF001

# webify
gulp.task "webify", () ->
  return gulp.src ["dist/svg/*.svg"]
    .pipe iconfont options
    .on "codepoints", (codepoints, options) ->
      for val in codepoints
        val.codepoint = val.codepoint.toString(16).toUpperCase()
      engine = "handlebars"
      consolidateOptions =
        glyphs: codepoints,
        fontName: options.fontName
        fontPath: "../fonts/"
        className: options.className

      gulp.src "src/template.css"
        .pipe consolidate engine, consolidateOptions
        .pipe concat "#{options.fontName}.css"
        .pipe gulp.dest "dist/css/"

      gulp.src "src/template.html"
        .pipe consolidate engine, consolidateOptions
        .pipe concat "index.html"
        .pipe gulp.dest "dist/"

    .pipe gulp.dest "dist/fonts/"
```

## gulp webserver

SVGを編集しながら、生成されたWebフォントをブラウザで確認するために、webserverのタスクも作りました。

`src/svg/*.svg`の変更を監視してminify,webifyが実行され、`dist`の変更を監視してlivereload機能が動きます。

[![debug画面][0001]][0001]
[0001]: https://raw.githubusercontent.com/YuG1224/gulp-task-svg-webify/master/doc/sample.png

``` coffee
gulp = require "gulp"
del = require "del"
rs = require("run-sequence").use(gulp)
webserver = require "gulp-webserver"

# watch
gulp.task "watch", () ->
  gulp.watch ["src/svg/*.svg"], () ->
    rs "clean", "minify", "webify"

# webserver
gulp.task "webserver", ["compile", "watch"], () ->
  gulp.src "dist"
    .pipe webserver
      livereload: true

# clean
gulp.task "clean", (done) ->
  del ["dist/*/*"], done

# initialize
gulp.task "initialize", (done) ->
  del ["dist"], done

# compile
gulp.task "compile", (done) ->
  rs "initialize", "minify", "webify", done
```

## まとめ

``` bash
$ gulp compile    // minify, webify を1回のみ実行
$ gulp webserver  // 変更を監視して、minify, webify を実行し、ブラウザ画面を自動更新
```

途中、`gulp-iconfont`が最低500x500以上のSVGが対象ということに気づかず、Webフォントが崩れてしまう現象にハマった。

素材が500x500以上というのが気になるけれど、ひとまずこれでIcoMoonにアップしなくてもローカル環境でWebフォント化することが出来るようになった。


